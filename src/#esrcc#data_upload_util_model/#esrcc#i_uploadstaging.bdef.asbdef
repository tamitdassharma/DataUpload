managed implementation in class /esrcc/bp_i_uploadstaging unique;
strict ( 2 );
with draft;
define behavior for /ESRCC/I_UploadStaging alias UploadStaging
persistent table /ESRCC/UPLD_STG
draft table /esrcc/d_upld
lock master
total etag LocalLastChangedAt
authorization master ( instance )
etag master LastChangedAt
{
  field ( mandatory : create ) Application;
  field ( numbering : managed ) UploadUIID;
  field ( readonly ) Status, UploadUIID, LastChangedAt, LastChangedBy, LocalLastChangedAt, CreatedBy, CreatedAt;
  field ( readonly : update ) Application;
  create;
  update;
  delete;


  validation validate_create on save {  field Filename; create; update;}

  // Logic to trigger action uploadExcelData
  determination uploadfields on save { create; field Filename; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;

  draft determine action Prepare{
   validation ( always ) validate_create;
  }

  mapping for /esrcc/upld_stg
  {
    UploadUIID = upload_uuid;
    DataStream = data_stream;
    MimeType = mime_type;
    Application = application;
    Filename = filename;
    TableName = table_name;
    Status = status;
    CreatedAt = created_at;
    CreatedBy = created_by;
    LocalLastChangedAt = local_last_changed_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
  }
}